declare_args() {
  use_private_channel_shared_lib = is_mac
  private_channel_flags = ""
}

private_channel_shared_lib_install_name = ""

if (use_private_channel_shared_lib && private_channel_flags == "") {
  if (!is_component_build) {
    private_channel_shared_lib_install_name =
        "@loader_path/../../Libraries/libprivate_channel.dylib"
    private_channel_flags = "-C link-args=-Wl,-install_name,$private_channel_shared_lib_install_name"
  }
}

private_channel_build_path = "$root_gen_dir/private_channel/out"

private_channel_lib_path = private_channel_build_path

private_channel_target = ""

# See https://forge.rust-lang.org/platform-support.html for possible targets
if (is_win) {
  if (current_cpu == "x86") {
    private_channel_flags += " -C target-feature=+crt-static"
    private_channel_target = "i686-pc-windows-msvc"
  } else if (current_cpu == "x64") {
    private_channel_target = "x86_64-pc-windows-msvc"
  }
} else if (is_mac) {
  if (current_cpu == "x64") {
    private_channel_target = "x86_64-apple-darwin"
  }
} else if (is_linux) {
  if (current_cpu == "x64") {
    private_channel_target = "x86_64-unknown-linux-gnu"
  }
} else if (is_android) {
  if (current_cpu == "arm") {
    private_channel_target = "arm-linux-androideabi"
  } else if (current_cpu == "arm64") {
    private_channel_target = "aarch64-linux-android"
  } else if (current_cpu == "x86") {
    private_channel_target = "i686-linux-android"
  } else if (current_cpu == "x64") {
    private_channel_target = "x86_64-linux-android"
  }
} else if (is_ios) {
  if (current_cpu == "arm64") {
    private_channel_target = "aarch64-apple-ios"
  } else if (current_cpu == "x64") {
    private_channel_target = "x86_64-apple-ios"
  }
}

if (private_channel_target != "") {
  private_channel_lib_path += "/" + private_channel_target
}

if (is_debug) {
  private_channel_lib_path += "/debug"
} else {
  private_channel_lib_path += "/release"
}

if (is_win) {
  private_channel_lib_path += "/private_channel.lib"
} else if (is_linux || is_android || is_ios) {
  private_channel_lib_path += "/libprivate_channel.a"
} else if (is_mac) {
  if (use_private_channel_shared_lib) {
    private_channel_lib_path += "/libprivate_channel.dylib"
  } else {
    private_channel_lib_path += "/libprivate_channel.a"
  }
}
